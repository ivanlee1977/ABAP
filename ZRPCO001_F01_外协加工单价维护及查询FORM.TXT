*&---------------------------------------------------------------------*
*&  包含                ZRPCO001_F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  GET_FILENAME
*&---------------------------------------------------------------------*
FORM get_filename  CHANGING p_path.

  DATA: l_v_file_filter  TYPE string.
  DATA: l_v_filetable TYPE file_table,
        l_t_filetable TYPE filetable.
  DATA: l_v_rc           TYPE i.
  DATA: l_v_user_action  TYPE i.

  l_v_file_filter = '(*.xlsx;*.xls)|*.xlsx;*.xls|'.
  cl_gui_frontend_services=>file_open_dialog(
     EXPORTING window_title = 'Select file'
               file_filter  = l_v_file_filter
     CHANGING  file_table   = l_t_filetable
               rc           = l_v_rc
               user_action  = l_v_user_action ).
  CHECK l_v_user_action EQ 0.
  CHECK l_t_filetable[] IS NOT INITIAL.
  READ TABLE l_t_filetable INTO l_v_filetable INDEX 1.
  p_path = l_v_filetable-filename.
ENDFORM.                    "get_filename

*&---------------------------------------------------------------------*
*&      Form  get_file_data
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_file_data .

  DATA:lt_upload LIKE alsmex_tabline OCCURS 0 WITH HEADER LINE.

  FIELD-SYMBOLS  <field>.
  FIELD-SYMBOLS: <l_fs_intern> TYPE alsmex_tabline."表头行
  FIELD-SYMBOLS: <l_fs_item> LIKE gw_item.
  DATA:l_structure TYPE REF TO data.

  CALL FUNCTION 'ALSM_EXCEL_TO_INTERNAL_TABLE'
    EXPORTING
      filename                = p_path
      i_begin_col             = 1
      i_begin_row             = 2
      i_end_col               = 20
      i_end_row               = 60000
    TABLES
      intern                  = lt_upload
    EXCEPTIONS
      inconsistent_parameters = 1
      upload_ole              = 2
      OTHERS                  = 3.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
        WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  CREATE DATA l_structure LIKE gw_item.
  LOOP AT lt_upload ASSIGNING <l_fs_intern>.
    AT NEW row.
      ASSIGN l_structure->* TO <l_fs_item>.
      INSERT gw_item INTO TABLE gt_item ASSIGNING <l_fs_item>.
    ENDAT.
    ASSIGN COMPONENT <l_fs_intern>-col OF STRUCTURE <l_fs_item>
           TO <field>.
    CHECK sy-subrc EQ 0.
    <field> = <l_fs_intern>-value.
  ENDLOOP.
ENDFORM.                    " GET_FILE_DATA

FORM output_excel USING im_objid."导出EXCEL模版 "im_objid接受来自perform语句的cs_objid的值
  DATA: lwk_object LIKE wwwdatatab, "表WWWDATA的内容,即通过smw0导入的模板的存储信息的表
        lwk_rc     TYPE sy-subrc,   "函数返回值,ABAP报表之后返回值
        lwk_ua     TYPE i.          "用于接收幼鹘Y果返回的值
  DATA: lwk_fn     TYPE rlgrap-filename. "用于装载/卸载的局部文件
  DATA: lwk_filename TYPE string.
  DATA: lwk_path TYPE string.
  DATA: lwk_fullpath TYPE string.

****---查询模板---***
  SELECT SINGLE relid objid  "relid: IMPORT/EXPORT 记录中的区域; objid:对象名
    FROM wwwdata             "存储 WWW 对象的 INDX 类型表
    INTO CORRESPONDING FIELDS OF lwk_object
    WHERE srtf2 = 0          "srtf2表示EXPORT/IMPORT 数据表中的下个记录计数器
      AND objid = im_objid.                                 "smw0里对象名称

  IF sy-subrc <> 0 OR lwk_object-objid = space . "如果没有找到模板或者模板文件名为空
    MESSAGE e000(zco01) WITH im_objid.  "004:模板文件&1不存在，请使用[SWM0]加载!
    STOP.  "停止事件
  ENDIF.

*---调用SAP操作window文件的事件（CL_GUI_FRONTEND_SERVICES），描述文件存放的路经，文件名，指定打开的文件，指定写入的文件---*
  CALL METHOD cl_gui_frontend_services=>file_save_dialog  "打开或者取得数据的文件地址的对话框
    EXPORTING
      window_title         = gwk_title       "对话框的标题
      default_extension    = gwk_extension   "默认的文件后缀名
      default_file_name    = gwk_filename    "默认打开的文件名
      initial_directory    = gwk_directory   "初始化的目录
*     multiselection       = 'X'       "是否可以同时打开多个文件
      file_filter          = cl_gui_frontend_services=>filetype_excel "文件打开的filter：文件类型excel
    CHANGING
      filename             = lwk_filename  "你所打开的文件名字列表
      path                 = lwk_path  "返回值,如果成功打开了,为1
      fullpath             = lwk_fullpath
      user_action          = lwk_ua  "幼鹘Y果,成功返回值0.
    EXCEPTIONS
      cntl_error           = 1
      error_no_gui         = 2
      not_supported_by_gui = 3
*     INVALID_DEFAULT_FILE_NAME = 4
      OTHERS               = 5.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ELSE.
    CHECK lwk_ua = 0.

    lwk_fn  = lwk_filename.

***---下载模板---***
    CALL FUNCTION 'DOWNLOAD_WEB_OBJECT'  "从服务器下载文件
      EXPORTING
        key         = lwk_object
        destination = lwk_fn   "文件名
      IMPORTING
        rc          = lwk_rc.  "系统返回值，下载成功则返回0.

    IF lwk_rc <> 0.
      MESSAGE e001(zco01) WITH im_objid.
      STOP.
    ENDIF.
  ENDIF.
ENDFORM.

*&---------------------------------------------------------------------*
*&      Form  CHECK_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM check_data.
  DATA:BEGIN OF wa_mara,
         matnr LIKE mara-matnr,
       END OF wa_mara.
  DATA:it_mara LIKE wa_mara OCCURS 0.
  "将物料号补前导0
  LOOP AT gt_item INTO gw_item.
    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
      EXPORTING
        input  = gw_item-zmatnr
      IMPORTING
        output = gw_item-zmatnr.
*系统用户和系统时间
    gw_item-ernam = sy-uname.
    gw_item-erdat = sy-datum.
    gw_item-uzeit = sy-uzeit.
    MODIFY gt_item FROM gw_item.
  ENDLOOP.
  CLEAR:sjjc.

  SELECT matnr INTO TABLE  it_mara FROM mara
     FOR ALL ENTRIES IN gt_item
    WHERE matnr EQ gt_item-zmatnr
      AND lvorm EQ space.

*判断是否存在没有权限的公司
  LOOP AT  gt_item INTO gw_item.
    READ TABLE it_mara INTO wa_mara WITH KEY matnr = gw_item-zmatnr.
    IF  sy-subrc NE 0.
      sjjc = 'X'.
*消息列表赋值
      gw_msg-zmatnr = gw_item-zmatnr.
      gw_msg-znetpr = gw_item-znetpr.
      CONCATENATE '此物料'gw_item-zmatnr'不存在' INTO gw_msg-message .
      APPEND gw_msg TO gt_msg.
    ENDIF.
  ENDLOOP .
  SORT gt_msg BY message.
  DELETE ADJACENT DUPLICATES FROM gt_msg COMPARING message.
ENDFORM.                    "CHECK_DATA

*&---------------------------------------------------------------------*
*&      Form  save_data
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM save_data.
****************消息提示******************
  CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      text = '开始批导数据'.
***********处理数据************************
  CLEAR msgtab[].
  CLEAR:gt_main[].
  LOOP AT  gt_item INTO gw_item.
*消息列表赋值
    gw_msg-zmatnr = gw_item-zmatnr.
    gw_msg-znetpr = gw_item-znetpr.
    gw_msg-message = '导入成功'.
    gw_msg-ernam = sy-uname.
    gw_msg-erdat = sy-datum.
    gw_msg-uzeit = sy-uzeit.
    APPEND gw_msg TO gt_msg.

    gw_main-zmatnr =  gw_item-zmatnr.
    gw_main-znetpr =  gw_item-znetpr.
    gw_main-ernam = sy-uname.
    gw_main-erdat = sy-datum.
    gw_main-uzeit = sy-uzeit.
    APPEND gw_main TO gt_main.
  ENDLOOP.
  MODIFY ztco001 FROM TABLE gt_main.
  IF sy-subrc = 0.
    COMMIT WORK AND WAIT .
  ELSE.
    MESSAGE '导入错误失败！'TYPE 'E'.
    sjjc = 'X'.
  ENDIF.
  CLEAR:gt_item.
ENDFORM.                    "save_data

*&---------------------------------------------------------------------*
*&      Form  output_log
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM output_log .
  DATA : col_pos TYPE i VALUE 1.

  CLEAR:it_fieldcat,wa_fieldcat.

  gs_layout-zebra = 'X'.
  gs_layout-cwidth_opt = 'X'."

  PERFORM fieldcat_int USING col_pos  'ZMATNR'   ' '   '物料号'          ''  'MATNR' 'MARA' '' ''.
  PERFORM fieldcat_int USING col_pos  'ZNETPR'   ' '   '外协加工单价（不含税）'   ''  '' '' ''   ''.
  PERFORM fieldcat_int USING col_pos  'ERNAM'   ' '   '导入数据人员'   ''  '' '' ''  '' .
  PERFORM fieldcat_int USING col_pos  'ERDAT'   ' '   '导入日期'   ''  '' '' ''  ''.
  PERFORM fieldcat_int USING col_pos  'UZEIT'   ' '   '导入时间'   ''  '' '' ''   ''.
  PERFORM fieldcat_int USING col_pos  'MESSAGE'   ' '   '批导信息'          ''  '' '' ''  '' .

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY_LVC'
    EXPORTING
      i_callback_program = sy-repid
      is_layout_lvc      = gs_layout
      it_fieldcat_lvc    = it_fieldcat
      i_save             = 'A'
    TABLES
      t_outtab           = gt_msg
    EXCEPTIONS
      program_error      = 1
      OTHERS             = 2.

  IF sy-subrc <> 0.
* MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*         WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.
ENDFORM.                    " OUTPUT_LOG
*&---------------------------------------------------------------------*
*&      Form  FIELDCAT_INT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_COL_POS  text
*      -->P_1122   text
*      -->P_1123   text
*      -->P_1124   text
*      -->P_1125   text
*      -->P_1126   text
*      -->P_1127   text
*      -->P_1128   text
*----------------------------------------------------------------------*
FORM fieldcat_int  USING   p_col_pos         " 1
                            p_fieldname       " 2
                            p_key             " 3
                            p_seltext_l       " 4
                            p_zero
                            p_filename
                            p_talbename
                            p_outputlen
                            p_edit .

  wa_fieldcat-col_pos       = p_col_pos.
  wa_fieldcat-fieldname     = p_fieldname.
  wa_fieldcat-key           = p_key.
  wa_fieldcat-coltext     = p_seltext_l.
  wa_fieldcat-no_zero        = p_zero.
  wa_fieldcat-ref_field   = p_filename.
  wa_fieldcat-ref_table    = p_talbename.
  wa_fieldcat-tipddictxt       = 'L'.
  wa_fieldcat-edit           = p_edit  .
  APPEND wa_fieldcat TO it_fieldcat.
  p_col_pos = p_col_pos + 1.
  CLEAR: wa_fieldcat.
ENDFORM.                    " FIELDCAT_INT

*&---------------------------------------------------------------------*
*&      Form  get_data
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_data.
*********取自建表数据**************************
  SELECT * INTO CORRESPONDING FIELDS OF TABLE gt_alv FROM ztco001
   WHERE zmatnr IN s_matnr.
  SORT gt_alv BY zmatnr .
  IF sy-ucomm EQ 'C_MODIFY'.
    PERFORM change_stutas.
  ELSE.
    LOOP AT gt_alv INTO gw_alv.
      stylelin-fieldname = 'ZNETPR'. " 需要编辑的列名
      stylelin-style = cl_gui_alv_grid=>mc_style_disabled. " 设置为可编辑状态
      INSERT stylelin INTO TABLE gw_alv-field_style.
      CLEAR stylelin.
      MODIFY gt_alv FROM gw_alv.
    ENDLOOP.
  ENDIF.
ENDFORM.                    "get_data


*&---------------------------------------------------------------------*
*&      Form  get_alv
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_alv.
  DATA : col_pos TYPE i VALUE 1.

  CLEAR:it_fieldcat,wa_fieldcat.

  gs_layout-zebra = 'X'.
  gs_layout-cwidth_opt = 'X'."
  gs_layout-stylefname = 'FIELD_STYLE'. " 将内表中的字段名存入显示格式
  gs_layout-box_fname        = 'SEL'.
  i_grid_settings-edt_cll_cb = 'X'.
*  wa_events-name = 'CALLER_EXIT'.
*  wa_events-form = 'CALLER_EXIT'.
*  APPEND wa_events TO it_events.

  PERFORM fieldcat_int USING col_pos  'ZMATNR'   ' '   '物料号'          ''  'MATNR' 'MARA' '' ''.
  PERFORM fieldcat_int USING col_pos  'ZNETPR'   ' '   '外协加工单价（不含税）'   ''  'NETPR' 'EKPO' '' 'X'.
  PERFORM fieldcat_int USING col_pos  'ERNAM'   ' '   '导入数据人员'   ''  '' '' ''   ''.
  PERFORM fieldcat_int USING col_pos  'ERDAT'   ' '   '导入日期'   ''  '' '' ''   ''.
  PERFORM fieldcat_int USING col_pos  'UZEIT'   ' '   '导入时间'   ''  '' '' ''   ''.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY_LVC'
    EXPORTING
      i_callback_program       = sy-repid
      is_layout_lvc            = gs_layout
      it_fieldcat_lvc          = it_fieldcat
      it_events                = it_events
      i_save                   = 'A'
      i_callback_pf_status_set = 'SET_PF_STATUS'
      i_callback_user_command  = 'ALV_USER_COMMAND'
      i_grid_settings          = i_grid_settings
    TABLES
      t_outtab                 = gt_alv
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.

  IF sy-subrc <> 0.
* MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*         WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.
ENDFORM.                    "get_alv

*&---------------------------------------------------------------------*
*&      Form  set_pf_status1
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->RT_EXTAB   text
*----------------------------------------------------------------------*
FORM set_pf_status USING rt_extab TYPE slis_t_extab.

  DATA: wa_extab TYPE slis_extab.

  SET PF-STATUS 'ZRPCO001_STATUS' EXCLUDING rt_extab.

ENDFORM.                    "set_pf_status1

FORM alv_user_command USING r_ucomm LIKE sy-ucomm  rs_selfield TYPE slis_selfield.
*   获取ALV中的鼠标勾选记录
  DATA: l_grid TYPE REF TO cl_gui_alv_grid.
  CALL FUNCTION 'GET_GLOBALS_FROM_SLVC_FULLSCR'
    IMPORTING
      e_grid = l_grid.
  rs_selfield-refresh = 'X'.
  rs_selfield-col_stable = 'X'.
  rs_selfield-row_stable = 'X'.

  r_ucomm = sy-ucomm.
  save_ok =  r_ucomm .
  CASE r_ucomm.
    WHEN 'C_MODIFY'.
      PERFORM get_data.
    WHEN 'C_SAVE'.
      PERFORM save_status.
    WHEN '&REFRESH'.
      PERFORM get_data.

  ENDCASE.


ENDFORM.                    "alv_user_command

FORM change_stutas.
  LOOP AT gt_alv INTO gw_alv WHERE sel EQ 'X'.
    stylelin-fieldname = 'ZNETPR'. " 需要编辑的列名
    stylelin-style = cl_gui_alv_grid=>mc_style_enabled. " 设置为可编辑状态
    INSERT stylelin INTO TABLE gw_alv-field_style.
    CLEAR stylelin.
    MODIFY gt_alv FROM gw_alv.
  ENDLOOP.
ENDFORM.


FORM save_status.
  CLEAR:gt_main[].
  LOOP AT  gt_alv INTO gw_alv.
    gw_main-zmatnr =  gw_alv-zmatnr.
    gw_main-znetpr =  gw_alv-znetpr.
    gw_main-ernam = sy-uname.
    gw_main-erdat = sy-datum.
    gw_main-uzeit = sy-uzeit.
    APPEND gw_main TO gt_main.
  ENDLOOP.
  MODIFY ztco001 FROM TABLE gt_main.
  IF sy-subrc = 0.
    COMMIT WORK AND WAIT .
    MESSAGE '保存数据成功！'TYPE 'S'.
  ELSE.
    MESSAGE '保存数据失败！'TYPE 'E'.
  ENDIF.
  CLEAR:gt_main.

ENDFORM.
